  mod DIE-HARD is
    protecting NAT .
    sorts Vasija ConjVasija .
    subsort Vasija < ConjVasija .

    op vasija : Nat Nat -> Vasija [ctor] .   *** Capacidad / Contenido actual
    op __ : ConjVasija ConjVasija -> ConjVasija [ctor assoc comm] .
    op initial : -> ConjVasija .

    vars M1 N1 M2 N2 : Nat .

    eq initial = vasija(3, 0) vasija(5, 0) vasija(8,0) .

    rl [vacia] : vasija(M1, N1) => vasija(M1, 0) .
    rl [llena] : vasija(M1, N1) => vasija(M1, M1) .
    crl [transfer1] : vasija(M1, N1) vasija(M2, N2)
      => vasija(M1, 0) vasija(M2, N1 + N2)
      if N1 + N2 <= M2 .
    crl [transfer2] : vasija(M1, N1) vasija(M2, N2)
      => vasija(M1, sd(N1 + N2, M2)) vasija(M2, M2)
      if N1 + N2 > M2 .
  endm

***(
Maude> search [1] initial =>* vasija(N:Nat, 4) B:ConjVasija .
search in DIE-HARD : initial =>* B:ConjVasija vasija(N:Nat, 4) .

Solution 1 (state 75)
states: 76  rewrites: 2134 in 0ms cpu (8ms real) (~ rewrites/second)
B:ConjVasija --> vasija(3, 3) vasija(8, 3)
N:Nat --> 5

Maude> show path 75 .
state 0, ConjVasija: vasija(3, 0) vasija(5, 0) vasija(8, 0)
===[ rl vasija(M1, N1) => vasija(M1, M1) [label fill] . ]===>
state 2, ConjVasija: vasija(3, 0) vasija(5, 5) vasija(8, 0)
===[ crl vasija(M1, N1) vasija(M2, N2) => vasija(M1, sd(M2, N1 + N2))
  vasija(M2, M2) if N1 + N2 > M2 = true [label transfer2] . ]===>
state 9, ConjVasija: vasija(3, 3) vasija(5, 2) vasija(8, 0)
===[ crl vasija(M1, N1) vasija(M2, N2) => vasija(M1, 0) vasija(M2, N1 + N2)
  if N1 + N2 <= M2 = true [label transfer1] . ]===>
state 20, ConjVasija: vasija(3, 0) vasija(5, 2) vasija(8, 3)
===[ crl vasija(M1, N1) vasija(M2, N2) => vasija(M1, 0) vasija(M2, N1 + N2)
  if N1 + N2 <= M2 = true [label transfer1] . ]===>
state 37, ConjVasija: vasija(3, 2) vasija(5, 0) vasija(8, 3)
===[ rl vasija(M1, N1) => vasija(M1, M1) [label fill] . ]===>
state 55, ConjVasija: vasija(3, 2) vasija(5, 5) vasija(8, 3)
===[ crl vasija(M1, N1) vasija(M2, N2) => vasija(M1, sd(M2, N1 + N2))
  vasija(M2,  M2) if N1 + N2 > M2 = true [label transfer2] . ]===>
state 75, ConjVasija: vasija(3, 3) vasija(5, 4) vasija(8, 3)
)